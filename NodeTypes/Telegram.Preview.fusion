prototype(Carbon.PaperTiger.Telegram:Action.Preview) < prototype(Neos.Fusion:Component) {
    config = ${Configuration.Setting('Carbon.PaperTiger.Telegram')}
    text = ${q(node).property('message')}
    apiKey = ${q(node).property('apiKey')}
    chatId = ${q(node).property('chatId')}
    allowableTags = ${this.config.allowableTags}

    @private {
        interfaceLanguage = ${Neos.Backend.interfaceLanguage()}
        message = ${Translation.translate('properties.message', 'Message', [], 'NodeTypes/Action', 'Carbon.PaperTiger.Telegram', null, private.interfaceLanguage)}
        apiKey = ${Translation.translate('properties.apiKey', 'API Key', [], 'NodeTypes/Action', 'Carbon.PaperTiger.Telegram', null, private.interfaceLanguage)}
        chatId = ${Translation.translate('properties.chatId', 'Chat ID', [], 'NodeTypes/Action', 'Carbon.PaperTiger.Telegram', null, private.interfaceLanguage)}
        requiredFields = Neos.Fusion:DataStructure {
            text = ${private.message}
            text.@if.isMissing = ${!props.text}

            apiKey = ${private.apiKey}
            apiKey.@if.isMissing = ${!props.apiKey && !props.config.apiKey}

            chatId = ${private.chatId}
            chatId.@if.isMissing = ${!props.chatId && !props.config.chatId}
        }
    }

    renderer = afx`
        <p @if={Array.length(private.requiredFields)} class="papertiger-action-telegram__error">
            <strong>
                {Translation.translate('configuration.missing', 'The following configuration is missing: {0}', [Array.join(private.requiredFields, ', ')], 'NodeTypes/Action', 'Carbon.PaperTiger.Telegram', null, private.interfaceLanguage)}
            </strong>
        </p>
        <dl class="papertiger-action-email__list">
            <dt @if={props.text}>{private.message}</dt>
            <dd @if={props.text}>{String.stripTags(props.text, props.allowableTags)}</dd>
            <dt @if={props.apiKey}>{private.apiKey}</dt>
            <dd @if={props.apiKey}>{props.apiKey}</dd>
            <dt @if={props.chatId}>{private.chatId}</dt>
            <dd @if={props.chatId}>{props.chatId}</dd>
        </dl>
    `
}
